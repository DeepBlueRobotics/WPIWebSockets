plugins {
  id "com.github.node-gradle.node" version "2.2.4"
}

apply plugin: 'java-library'
apply plugin: 'jacoco'
apply plugin: 'maven-publish'

group = 'org.carlmontrobotics'
repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    compile group: 'org.java-websocket', name: 'Java-WebSocket', version: '1.5.1'
    //Add SLF4J NOP Logger Binding to prevent runtime warnings
    compile group: 'org.slf4j', name: 'slf4j-nop', version: '1.7.30'
    compile group: 'com.github.cliftonlabs', name: 'json-simple', version: '3.1.1'
    testImplementation 'junit:junit:4.13'
}

def templateDir = "${projectDir}/asyncapi-template"

task archiveTemplate(type: Tar) {
    archiveFileName = "asyncapi-template.tar"
    destinationDirectory = file("$buildDir/tmp")

    from(templateDir) {
      exclude "node_modules"
      into "asyncapi-template"
    }
}

// Add a task to generate java source code for the devices using npx to run
// the asyncapi generator.
task generateDeviceFiles(type: NpxTask) {
    def outputDir = "${buildDir}/generated/sources/asyncapi/org/team199/wpiws/devices"

    // Define the command line that npx should use
    workingDir = buildDir // Because templateDir can't be under it
    command = '@asyncapi/generator'
    args = ['--force-write', '-o', outputDir, "${templateDir}/wpilib-ws.yaml", "$buildDir/tmp/asyncapi-template.tar"]

    // Define the inputs and outputs of this task so that gradle only runs it 
    // when necessary.
    inputs.files(archiveTemplate.outputs)
    outputs.dir(outputDir).withPropertyName("outputDir")

}

// Include the generated files in the source to compile.
sourceSets.main.java.srcDirs generateDeviceFiles.outputs

// Use a version of node and npm that is known to work.
node {
  download = true
  version = '14.15.1'
  npmVersion = '6.14.8'
}
